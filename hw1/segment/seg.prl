#!/usr/bin/perl -w


##ITEM 1234 and SIG!!!!!!!
use strict;

############################################################
##  seg_examp1.prl
##
##  seg_examp1.prl < segment.data.train
## 
##
##  Example of how one may begin a simple version of this
##  program. Lines of text are read into a @test_lines array
##  of lines (with length measured in $numb_lines) until a 
##  blank line is reached. Then the lines in the previous 
##  segment are processed.  The array @real_lines stores the
##  values of the correct classifications for the given in-
##  put data, useful for comparison with the segment class-
##  ifications chosen by the program.
############################################################

my @old_test_lines = ();
my @old_real_lines = ();
my @test_lines = ( );    # the current line that we are testing
my @real_lines = ( );    # the correct classification for the current line
my $numb_lines = 0;      # the number of lines in the block that we testing
my $old_numb_lines = 0;
my $result = undef;
my $current_result = undef;
my $correct   = 0;       # number of correct classifications
my $incorrect = 0;       # number of incorrect classifications
my $line      = undef;
my $old_line = "";

# /\([0..9]{3}\)\s[0..9]{3}\-[0..9]{4}/
# /^[0..9]{5}\-[0..9]{4}$/

sub loadfile {
    my $path = shift;
    my @a = ();
    open(FILE, $path) || die "Cannot open file $path";
    while (<FILE>) {
        chop;
        $a[$#a + 1] = $_;
    }
    return join("|", @a);
}
my $placeregex = &loadfile("places");
my $contactregex = &loadfile("contact");
my $addressregex = &loadfile("address");



while (defined ($line = <STDIN>)) {
    chomp $line;
    if ($line =~ /^#BLANK#/) {

        if ($numb_lines) {
            $current_result = &process_segment;     # if we are at a blank line then
                                                 # it is time to process the pre-
                                                # vious block
            if ($result) {
                if ($current_result eq "NNHEAD") {
                    if ($result eq "GRAPHIC") {
                        &detected("SIG");
                    }
                    elsif ($result eq "ITEM" && scalar @old_test_lines == 1) {
                        &detected("SIG");
                    } else {
                        &detected($result);
                    }
                } else {
                    &detected($result);
                }
            }
            $result = $current_result;
            @old_test_lines = @test_lines;
            @old_real_lines = @real_lines;
            print $old_line;
            $old_line = "";

            @test_lines  = ( );   # make sure that we clear memory here or else
            @real_lines  = ( );   #  these arrays may hold old data which will
            $numb_lines  = 0;     #  screw up calculations of their length
        }
        $old_line = $old_line . $line. "\n";

    }
    else {

        ($test_lines[ $numb_lines ]) = ($line =~ m/^[^\t]+\t(.*)/);
        ($real_lines[ $numb_lines ]) = ($line =~ m/^([^\t]+)\t/  );

        $numb_lines += 1;
    }
    
}
print @test_lines;
# @old_test_lines = @test_lines;
# @old_real_lines = @real_lines;
if ($current_result eq "GRAPHIC") {
    &detected("SIG");
} else {
    &detected($current_result);
}
print $old_line;
print "CORRECTNESS: ", $correct / ($correct + $incorrect), "\n";
############################################################
## Process segments by applying specialized detectors/
## classifiers to the array of the text lines in the segment 
############################################################


sub process_segment {
    my $cnt;
    if ( &quoted_detector ) {
        # &detected("QUOTED");
        return "QUOTED";
    } 

    elsif (&head_detector) {
        # &detected("NNHEAD");
        return "NNHEAD";
    }
    elsif (&graphic_detector) {
        # &detected("GRAPHIC");
        return "GRAPHIC";
    }
    elsif (&address_detector) {
        # &detected("ADDRESS");
        return "ADDRESS";
    }
    elsif (&headline_detector) {
        # &detected("HEADL");
        return "HEADL";
    }
    elsif (&table_detector) {
        # &detected("TABLE");
        return "TABLE";
    }
    elsif (&item_detector) {
        # &detected("ITEM");
        return "ITEM";
    }   
    # elsif (&sig_detector) {
    #     &detected("SIG");
    # }
    else {
        # &detected("PTEXT");
        return "PTEXT";
  }
}

sub detected {
    my $label = shift;
    for (my $cnt = 0; $cnt < scalar @old_test_lines; $cnt++) {
        &ret(0, $old_real_lines[$cnt], $label, $old_test_lines[$cnt]);
    }
}
############################################################
## This is an incomplete example of what a quotation detector
## might look like.
############################################################

sub table_detector {
    my $weight = 0.75;
    my $wordsum = 0;
    my $charsum = 0;
    my $wordlenscore = 0;
    my $charlenscore = 0;
    my $linespacescore = 0;
    my $linetabscore = 0;
    if (scalar @test_lines <= 1) {
        return 0;
    }
    for (my $cnt = 0; $cnt < scalar @test_lines; $cnt++) {
        my $spacesum = 0;
        my $tabsum = 0;
        my @words = split(/\s*/, $test_lines[$cnt]);
        $wordsum += scalar @words;
        $charsum += length $test_lines[$cnt];

        my @spaces = split(/[^\s]*/, $test_lines[$cnt]);
        my @tabs = split(/[^\t]*/, $test_lines[$cnt]);
        
        for (my $i = 0; $i < scalar @spaces; $i++) {
            $spacesum += length $spaces[$i];
        }
        for (my $i = 0; $i < scalar @tabs; $i++) {
            $tabsum += length $tabs[$i];
        }
        if (scalar @words <= 3 && $tabsum >= scalar @words || $tabsum >= 3) {
            $linetabscore++;
        }
        if ($spacesum / length $test_lines[$cnt] >= 0.35) {
            $linespacescore++;
        }
        # print "spacenum: $spacesum\nlength: ", length $test_lines[$cnt], "\n";
    }
    $wordsum /= scalar @test_lines;
    $charsum /= scalar @test_lines;
    for (my $cnt = 0; $cnt < scalar @test_lines; $cnt++) {
        my @words = split(/\s*/, $test_lines[$cnt]);
        if (scalar @words >= $wordsum - 1 && scalar @words <= $wordsum + 1) {
            $wordlenscore++;
        }
        if (length $test_lines[$cnt] >= $charsum - 0.5 && length $test_lines[$cnt] <= $charsum + 0.5) {
            $charlenscore++;
        }
    }
    # print "wordlenscore: $wordlenscore\ncharlenscore: $charlenscore\nlinespacescore: $linespacescore\n";
    return ((0.3 * $wordlenscore + 0.5 * $charlenscore + $linespacescore + $linetabscore) / scalar @test_lines >= $weight);

}

sub trim {
   return $_[0] =~ s/^\s+|\s+$//rg;
}
sub leadingspace {
    my $str;
    if ($_[0] =~ /(^\s+)/) {
        $str = $1;
    } else {
        $str = "";
    }
    return length($str);
}



sub indentsamelevel {
    my @spacelen = ();
    my $equalspaces = 1;
    for (my $cnt = 0; $cnt < scalar @test_lines; $cnt++) {
        $spacelen[$#spacelen + 1] = &leadingspace($test_lines[$cnt]);
    }
    for (my $i = 0; $i < (scalar @spacelen) - 1; $i++) {
        if ($spacelen[$i] != $spacelen[$i + 1]) {
            $equalspaces = 0;
        }
    }
    return $equalspaces;

}
sub address_detector {
    my $contact_score = 0;
    my $address_score = 0;
    my $place_score = 0;
    my $shortlines = 0;
    my $score = 0;
    my @spacelen = ();

    for (my $cnt = 0; $cnt < scalar @test_lines; $cnt++) {
        my @words = split(" ", $test_lines[$cnt]);

        if (length(&trim($test_lines[$cnt])) <= 35) {
            $shortlines++;
        }
        for (my $wordcnt = 0; $wordcnt < scalar @words; $wordcnt++) {
            if (lc($words[$wordcnt]) =~ /$contactregex/) {
                $contact_score = 1;
            }
            if ($words[$wordcnt] =~ /$addressregex/) {
                $address_score = 1;
            }
            if ($words[$wordcnt] =~ /$placeregex/) {
                $place_score = 1;
            }
        }
        # print "charcnt: $charcnt\n";
    }
     
    return &indentsamelevel && ($shortlines / scalar @test_lines >= 0.8) && ($contact_score + $address_score + $place_score >= 2);
    
}
sub item_detector {
    my $score = 0;
    my $weight_score = 0.75;
    for (my $cnt = 0; $cnt < scalar @test_lines; $cnt++) {
        if ($test_lines[$cnt] =~ /^\t*\s*\-/) {
            $score++;
        }
        # if ($test_lines[$cnt] =~ /^\s*[0-9]\.\s|^\s*\([0-9]\)\s|^\s*[0-9]\)\s/) {
        if ($test_lines[$cnt] =~ /^\s*[0-9]\.\s|^\s*\([0-9]\)\s/) {
            $score += 10;
        }
    }
    return ($score / scalar @test_lines >= $weight_score);
}

sub graphic_detector {
    my $num_char = 0;
    my $num_symbol = 0;
    my $score = 0;
    my $weight_sym_ratio = 0.5;
    my $weight_rep_char = 0.5;
    my $weight_score = 0.51;
    my $repcharword = 0;
    for (my $cnt = 0; $cnt < scalar @test_lines; $cnt++) {
        my @words = split(" ", $test_lines[$cnt]);
        $num_char = 0;
        $num_symbol = 0;
        $repcharword = 0;
        for (my $word_cnt = 0; $word_cnt < scalar @words; $word_cnt++) {
            my @chars = split(//, $words[$word_cnt]);
            for (my $char_cnt = 0; $char_cnt < scalar @chars; $char_cnt++) {
                $num_char++;
                if ($chars[$char_cnt] =~ /[~|@|#|\$|%|\^|&|\*|\(|\)|\-|_|=|\+|\[|\]|:|;|"|'|<|>|,|\.|\/|\?|\\|\|]/) {
                    $num_symbol++;
                }
            }
            my $fc = $chars[0];
            if (scalar @chars > 1 && $fc =~ /[A-Za-z]/ && $words[$word_cnt] =~ /^$fc+$/) {
                $repcharword++;
            }
        }
        # print "repcharword: $repcharword\n";
        # print "num_char: $num_char, num_symbol, $num_symbol\n";
        # print "ratio: ", $num_symbol / $num_char, "\n";
        if ($num_symbol / $num_char >= $weight_sym_ratio || $repcharword / scalar @words >= $weight_rep_char) {
            $score++;
        }
    }
    return ($score / scalar @test_lines >= $weight_score);
}

sub sig_detector {
    my $sym_line = 0;
    for (my $cnt = 0; $cnt < scalar @test_lines; $cnt++) {
        my @words = split(" ", $test_lines[$cnt]);
        my $num_symbol = 0;
        for (my $word_cnt = 0; $word_cnt < scalar @words; $word_cnt++) {
            my @chars = split(//, $words[$word_cnt]);
            for (my $char_cnt = 0; $char_cnt < scalar @chars; $char_cnt++) {
                if ($chars[$char_cnt] =~ /[\^|_|=|\[|\]|\/|\\|\|]/) {
                    $num_symbol++;
                }
            }
        }
        if ($num_symbol >= 3) {
            $sym_line++;
        }
    }

    return ($sym_line / scalar @test_lines >= 0.5);

}




sub headline_detector {
    my $weight1 = 0.75;
    my $weight2 = 0.5;
    my $score = 0;
    my $linesymbols = 0;
    my $lineallcaps = 0;
    for (my $cnt = 0; $cnt < scalar @test_lines; $cnt++) {
        my $spaces = "";
        ($spaces) = ($test_lines[$cnt] =~ /^(\s+)/);
        my @words = split(" ", $test_lines[$cnt]);
        my $caps = 0;
        my $allcaps = 0;
        foreach (@words) {
            if ($_ =~ /^[A-Z0-9]/ || $_=~ /at|or|and|in|of|on|the/) {
                # print "haha";
                $caps += 1;      
            }
            if ($_ =~ /^[A-Z0-9]*$/) {
                $allcaps += 1;
            }
        }
        if ($test_lines[$cnt] =~ /\s*^[\-{3,}|={3,}]$ \s*/) {
            $linesymbols += 1;
        }
        if ($allcaps == scalar(@words)) {
            $lineallcaps += 1;
        } elsif (($caps / scalar(@words) >= $weight1) && ($spaces && length($spaces) >= 1)) {
            $score += 1;
        }

    }
    # print "SCORE: $score\n";
    return (($linesymbols * 2 + $lineallcaps * 2 + $score) / scalar(@test_lines) >= $weight2);


}
sub head_detector {
    my $weight = 5;
    my $from = "^From.+";
    my $article = "^Article:.+";
    my $path = "^Newsgroups:.+";
    my $subject = "^Subject:.+";
    my $message_id = "^Message-ID:.+";
    my $date = "^Date:.+";
    my $sender = "^Sender:.+";
    my $dist = "^Distribution:.+";
    my $organ = "^Organization:.+";
    my $lines = "^Lines:.+";
    my %feats = (
        $from => 0,
        $article => 0,
        $path => 0,
        $subject => 0,
        $sender => 0,
        $message_id => 0,
        $date => 0,
        $dist => 0,
        $organ => 0,
        $lines => 0
    );

    
    for (my $cnt = 0; $cnt < scalar @test_lines; $cnt++) {
        for my $key (keys %feats) {
            if ($test_lines[$cnt] =~ /$key/) {
                $feats{$key} += 1;
            }
        }
    }

    my $score = 0;
    for my $key (keys %feats) {
        if ($feats{$key} > 0) {
            $score += 1;
        }
    }
    
    return $score >= $weight;
}

    
sub quoted_detector {

    my $score  = 0;
    my $weight = 0.75;   # weight is a variable so that it is easy to find
    my $cnt;             #  and change.

    # I like to assign regex's to variable names so that I can later
    # build very large and complex regex's with little space. This 
    # also forces perl to compile the regex's once only which speeds
    # things up dramatically
    #
    # note that each regex is built from a "..." string. This means 
    # each regex is interpolated which can have some unexpected results
    # with regards to perl meta characters (see $quote5 below).

    my $quote1 = ">";
    my $quote2 = ":";
    my $quote3 = "\\|>"; # so we need to quote meta twice here becasue this
                         # regex's is evaluated first here and then at $block-
                         # _quote below.
                         # 
                         # I could probably solve this problem by using single
                         # quotes here, but I still have to quote meta once
                         # for the regex below (which must use double quotes).
    my $quote4 = "\\@";
    # these are compilations of the above regex's into a single regex.
    # The "..." quotes means that each regex is interpolated which is
    # why I "\\|" above so that "|" is interpreted to mean '|' and not
    # "or".
    #
    # if I were just searching for the above three delimiters it would have
    # been more efficient for me to use a case statement like check
    #
    #   $score++ and next if $test_lines[$cnt] =~ /^$quote1/;
    #   $score++ and next if $test_lines[$cnt] =~ /^$quote2/;
    #   etc. ...
    #
    # but I noticed from the test data that many "QUOTED" delimiters are
    # compilations of the above three and to write each of those out would
    # require 3 * 2^n permutations (n being the number of repeats of a 
    # delimiter). Here I do the whole thing with only one regex.

    my $block_quote1 = "$quote1|$quote2|$quote3|$quote4";
    my $block_quote2 = "$quote1|$quote2|$quote4";

    # the below regex will grab strings of the form
    #
    #   ">  some quote ... "
    #   ":  some quote ... "
    #   "|> some quote ... "
    #
    # as well as regex's of the form
    #
    #   ": > some quote ... "
    #   ">>> some quote ... "
    #   etc. ...

    my $test_quote1  = "^($block_quote1)(\\s*($block_quote2))*";
    my $test_quote2  = "^.+((<.*\\@.*>)|(\(.*\\@.*\))).+(wrote|writes):";
    my $test_quote3  = "^In article\\s<.*\\@.*>";
    my $prefix = "";
    if ($test_lines[0] =~ /^\s*([a-zA-Z]{1,5}\s*>)\s*/) {
        $prefix = $1;
    }
    for ($cnt = 0; $cnt < scalar @test_lines; $cnt++) {

        if ( $test_lines[$cnt] =~ /$test_quote1/ ||
             $test_lines[$cnt] =~ /$test_quote2/ ||
             $test_lines[$cnt] =~ /$test_quote3/ ||
             (($prefix ne "") && ($test_lines[$cnt] =~ /^\s*$prefix\s*/))) {
            $score++;
        }
    }

    return (($score / (scalar @test_lines)) > $weight);
}


############################################################
## RET - prints full line for errors and keeps score $rule
##     - indicates the rule number responsible for the 
##     - classification
##
##  This is a useful debugging tool, as after each change in
##  the program one can identify the remaining misclassified
##  training examples and the patterns responsible for them. 
############################################################
 
sub ret {

    my $rule       = shift;   # which rule did I use here
    my $trueclass  = shift;   # what is the true sentence class ('NEOS','EOS')
    my $givenclass = shift;   # what is the sentence class my rule gives
    my $line       = shift;   # the actual line tested

    if ($trueclass eq $givenclass) {

        printf(".. (%2d) %s\t%s\t%s\n",$rule,$givenclass,$trueclass,$line);
        $correct++;
    }
    else {
	
        printf("XX (%2d) %s\t%s\t%s\n",$rule,$givenclass,$trueclass,$line);
        $incorrect++;
    }
}

